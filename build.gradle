plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.3")
    //통합 테스트에서 경량 레디스 컨테이너를 사용하기 위해 추가
    set('testcontainersVersion', "1.19.8")
}

dependencies {
    // Spring Reactive Stack을 기반으로 만들어졌으며, 요청을 API로 라우팅하고 복원력, 보안, 모니터링 같은 공통 문제에 대한 유틸리티 제공
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    //Spring Cloud Circuit Breaker 사용을 위한 Resilience4J의 Reactive Version을 의존성에 추가
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j'
    //인 메모리 비관계 데이터 저장소인 레디스를 지원하는 Spring Data Redis 사용, Imperative & Reactive 모두 지원
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
    //Spring에서 Session 관리를 위해 Spring Session Data Redis 사용
    implementation 'org.springframework.session:spring-session-data-redis'
    //config-service로 부터 설정 데이터를 가져오기 위한 Spring Cloud Config Client를 위해 추가
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    //Netty는 성능상 이점을 얻기 위해 네이티브 코드를 사용해 시스템의 DNS 리졸버와 연동하는데, ARM 기반의 칩에서는 필요한 라이브러리가 없어서 이 native 라이브러리들을 직접 명시적으로 제공해줘야 한다.
    runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.1.104.Final:osx-aarch_64'

    //Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    //TestContainers
    testImplementation 'org.testcontainers:junit-jupiter'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        //TestContainers
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

bootBuildImage {
    builder = "docker.io/paketobuildpacks/builder-jammy-base"
    imageName = "${project.name}"
    environment = ["BP_JVM_VERSION": "17"]

    docker {
        publishRegistry {
            username = project.findProperty("registryUsername")
            password = project.findProperty("registryToken")
            url = project.findProperty("registryUrl")
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
